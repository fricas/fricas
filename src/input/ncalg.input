)set break resume
)expose UnittestCount UnittestAux Unittest

testsuite "ncalg"

testcase "fractions"

ALPHABET := ['x, 'y, 'z];
OVL := OrderedVariableList(ALPHABET)
F := Fraction(Integer)
OFM := FreeMonoid(OVL)
FDA := FDALG(OVL, F)

x := 'x::OFM::FDA;
y := 'y::OFM::FDA;
z := 'z::OFM::FDA;

testEquals("(x^-1 + y^-1)^-1 - y*(x + y)^-1*x", "0")
testEquals("x - (x^-1 + (y^-1 - x)^-1)^-1 - x*y*x", "0")

f1 := x + y
f2 := x + 3*y
f3 := x - y

testEquals("((f1 + f2) + f3) - (f1 + (f2 + f3))", "0")
testEquals("((f1 + f2^-1) + f3) - (f1 + (f2^-1 + f3))", "0")
testEquals("(f1*f2)*f3 - f1*(f2*f3)", "0")
testEquals("(f1*f2^-2)*f3 - f1*(f2^-2*f3)", "0")
testEquals("(f1 + f2)*f3 - f1*f3 - f2*f3", "0")
testEquals("(f1 + f2^-1)*f3 - f1*f3 - f2^-1*f3", "0")

testcaseNoClear "admissible linear systems"

als ==> admissibleLinearSystem

-- preparing some admissible linear systems representing zero
f_0 := 0$FDA;
f_1 : FDA := qnew(2);
f_2 : FDA := qnew(3);
for k in 2 .. 3 repeat _
  qsetelt!(pencil(f_1), k, k, 1, 1$F)
for k in 2 .. 4 repeat _
  qsetelt!(pencil(f_2), k, k, 1, 1$F)
f_3 := copy(f_2);
addRows!(f_3, 2, 3, 2);
f_4 : FDA := x*y*z;
f_4(4) := 0;

-- preparing some admissible linear systems
f_5 : FDA := x*y*z;
f_6 := copy(f_5);
f_6 := addRows!(f_6, 3, 4, 2);
f_7 := insertRowsColumns(f_5, [1], [2]);
f_8 : FDA := x*z*y;

-- preparing some big admissible linear systems
f_11 : FDA := x*y*x*y*x*y*x*y + z
f_12 : FDA := x*y*x*y*x*y*x*y + x
f_13 := invertMIN(f_11)
f_14 := invertMIN(f_12)

-- prepare some tiny admissible linear systems
f_15 : FDA := z^-1;
f_16 := insertRowsColumns(f_15, [0,1], [0,1]);
qsetelt!(pencil(f_16), 2, 3, 1, -2);
qsetelt!(pencil(f_16), 2, 4, 1, -1/2);
f_16(1) := 1;
f_16(2) := 0;
f_16(3) := -2;

-- check minimization
als(minimize(f_1))
als(minimize(f_2))
als(minimize(f_3))
als(minimize(f_4))
als(minimize(f_16))

-- simple tests (zero)
testTrue("zero?(f_0)")
testTrue("zero?(f_1)")
testTrue("zero?(f_2)")
testTrue("zero?(f_3)")
testTrue("zero?(f_4)")

-- simple tests (true)
testEquals("f_0", "f_0")
testEquals("f_1", "f_0")
testEquals("f_0", "f_2")
testEquals("f_1", "f_4")
testEquals("f_5", "f_6")
testEquals("f_5", "f_7")
testEquals("f_7", "f_6")

-- simple tests (false)
testTrue("not(f_5 = f_8)")
testTrue("not(f_8 = f_6)")
testTrue("not(f_8 = f_7)")

-- tests which depend on linear word problem (false)
testTrue("not(f_13 = f_14)")

testcaseNoClear "factoring"

XDP ==> XDPOLY(OVL, F)
XPF ==> XPolynomialFactor(OVL, F)
lS ==> List(Symbol)

x := 'x::OFM::XDP;
y := 'y::OFM::XDP;
z := 'z::OFM::XDP;

XDP_to_FDA(p) ==
    lt := listOfTerms(p)
    res : FDA := 0
    for t in lt repeat
        res := res + (t.c)::FDA*(t.k)::FDA
    res

e1 := ['mult1, 'mult3]$lS
e1p3 := ['mult1, 'prop3]$lS
e1o3 := ['mult1, 'ord3]$lS
e2 := ['mult1, 'mult2, 'mult3]$lS
p2 := ['mult1, 'prop2, mult3]$lS
p23 := ['mult1, 'prop2, 'prop3]$lS
p3 := ['mult1, 'mult2, 'prop3]$lS
e3 := ['mult3]$lS

check_factor_prop(nl1, nl) ==
    nl2 : List(FDA) := factor(nl1)
    for fi2 in nl2 for fi in nl repeat
        if not(scalar?(fi2/fi)) then return false
    true

check_factor_prop3(x1, lx) ==
    lx2 := factor(x1)$XPF
    for fi2 in lx2 for fi1 in lx repeat
        lc2 := leadingCoefficient(fi2)
        lc1 := leadingCoefficient(fi1)
        if not(lc2*fi1 = lc1*fi2) then
            print fi1::OutputForm
            print fi2::OutputForm
            return false
    true

check_factor_ord3(x1, lx) ==
    lx2 := factor(x1)$XPF
    for fi2 in lx2 repeat
        res := false
        for fi1 in lx for k in 1.. repeat
            lc2 := leadingCoefficient(fi2)
            lc1 := leadingCoefficient(fi1)
            not(lc2*fi1 = lc1*fi2) => "iterate"
            res := true
            break
        not res =>
            print fi2::OutputForm
            print lx::OutputForm
            return res
        lx := delete(lx, k)
    true

test_factor(l: List XDP, ol : lS): Void ==
    free lx
    free l1
    free nl
    free nl1
    lx := l
    nl := [XDP_to_FDA(p) for p in l]
    l1 := reduce(*, l)
    nl1 := reduce(*, nl)
    if member?('mult1, ol) then
        testEquals("factor nl1", "nl")
    if member?('mult2, ol) then
        nl1 := XDP_to_FDA(l1)
        testEquals("factor nl1", "nl")
    if member?('prop2, ol) then
        nl1 := XDP_to_FDA(l1)
        testTrue("check_factor_prop(nl1, nl)")
    if member?('mult3, ol) then
        testEquals("factor(l1)$XPF", "lx")
    if member?('prop3, ol) then
        testTrue("check_factor_prop3(l1, lx)")
    if member?('ord3, ol) then
        testTrue("check_factor_ord3(l1, lx)")

a2 := x*y - y*x
a3 := x*y*z - x*z*y + z*x*y - z*y*x + y*z*x - y*x*z

test_factor([x, y], e2)
test_factor([x, 1+y*x], e2)
test_factor([1 - x*y, 1 - y*x], p3)
test_factor([1 - x*y], e2)
test_factor([x*y-y*x], e2)
test_factor([x*y+y*x], e2)
test_factor([1 - x*y*z*y*x*z], e2)
test_factor([x - 3, x - 3], p2)
test_factor([2*x - 3, 2*x - 3], p23)
test_factor([x*x-2], e2)
test_factor([x+y, x-y], e2)
test_factor([x, 1 + y*x, 1+z*y, x*y*z-z*x*y], e2)
test_factor([1-x*y, 2+y*x, 3-y*z, 2-z*y, 1-x*z], e1p3)
test_factor([3*x*y+5*y*x, 3*x*y+5*y*x], p23)
test_factor([3*x*y+5*y*x, 3*x*y+5*y*x, 3*x*y+5*y*x], p23)
test_factor([2 + x, (1/2)*(1 + y), -6 + 2*z], p3)
test_factor([1-x*y, 2+y*x, 3-y*z, 2-z*y, x], p23)
test_factor([(1/12)*(x^2 + 5), 12*(x^2 + x + 7)], p23)
test_factor([-(29/3)*(3*x^2 - x - 7), -(3/29)*(2*x^2 +3*x - 4)], e1p3)
test_factor([(1/5)*(x^2 + 5), 5*(x^3 + x + 7)], e1p3)
test_factor([x^3 + 5, x^3 + x + 7], e1)
test_factor([(1/7)*(x^3 + x + 7), 7*a3, a2], e1p3)
test_factor([a2^2 - 2, a2^2 - 3], e1o3)
test_factor([a3, 1 + y], e2)
test_factor([x^4 + 5, x^4 + x + 7], e3)
test_factor([a2^2 - 5, a2^2 - 3, a2^2 - 2], e3)
test_factor([(x^2+y)^4 + 5, (x^2+y)^4 + (x^2+y) + 7], e3)

testcase "xpoly"

xD := XDPOLY(OVAR([r, s, t]), INT)
fM := FreeMonoid(OVAR([r, s, t]))
rd := monomial(1, r::fM)$xD
sd := monomial(1, s::fM)$xD
td := monomial(1, t::fM)$xD

pd1 := sd*td*sd
-- used to fail
testTrue("(w1 := retract(pd1)@fM; true)")
testEquals("pd1", "monomial(1, w1)$xD")
uFM := Union(fM, "failed")
testEquals("retract(pd1*pd1)@fM", "w1*w1")
testTrue("not(retractIfCan(2*pd1)@uFM case fM)")
testTrue("not(retractIfCan(pd1 + 1)@uFM case fM)")
testLibraryError("retract(pd1 + rd)@fM")

xR := XRPOLY(OVAR([r, s, t]), INT)
rr := monomial(1, r::fM)$xR
sr := monomial(1, s::fM)$xR
tr := monomial(1, t::fM)$xR

pr1 := monomial(1, w1)$xR
testEquals("pr1", "unexpand(pd1)$xR")
testEquals("retract(pr1)@fM", "w1")

pr2 := rr*(sr+tr + 1)*(tr + rr)
pd2 := expand(pr2)
testEquals("pr2", "unexpand(pd2)$xR")
pr3 := (sr+1)*(tr -1)*(rr + 3)
testEquals("numberOfMonomials(pr3)", "8")
pd3 := expand(pr3)
testEquals("pr3", "unexpand(pd3)$xR")
testEquals("leadingCoefficient(pr3)", "1")
testEquals("leadingSupport(pr3)", "s::fM * t::fM * r::fM")
testEquals("expand(pr2*pr3)", "pd2*pd3")
testEquals("sh(pr2, pr3)", "unexpand(sh(pd2, pd3))$xR")
testEquals("sh(pr1, pr3)", "sh(pr3, pr1)")
testEquals("expand(mirror(pr3))", "mirror(expand(pr3))")
testEquals("mirror(pr3)*pr3", "mirror(mirror(pr3)*pr3)")

xDR := XRPOLY(OVAR([x, y]), xD)
xx := monomial(1, x::FreeMonoid(OVAR([x, y])))$xDR
yx := monomial(1, y::FreeMonoid(OVAR([x, y])))$xDR
-- used to fail
testEquals("(xx*sx)*tx", "xx*(sx*tx)")
testEquals("(xx*sx)*tx", "sx*tx*xx")

xRf := XRPOLY(OVAR([x, y]), PF(5))
xr := monomial(1, x::FreeMonoid(OVAR([x, y])))$xRf
-- used to fail
testEquals("sh(xr*xr*xr, xr*xr*xr)", "0")

statistics()
