--Copyright The Numerical Algorithms Group Limited 1994.

-- Color gallery page 5


-- Etruscan Venus
-- Parameterization by George Frances

venus(a,r,steps) ==
  surf := (u:DoubleFloat, v:DoubleFloat): Point DoubleFloat +->
    cv := cos(v)
    sv := sin(v)
    cu := cos(u)
    su := sin(u)
    x := r * cos(2*u) * cv + sv * cu
    y := r * sin(2*u) * cv - sv * su
    z := a * cv
    point [x,y,z]
  draw(surf, 0..%pi, -%pi..%pi, var1Steps==steps,var2Steps==steps,
       title == "Etruscan Venus")

venus(5/2, 13/10, 50)

-- Figure Eight Klein Bottle
-- Parameterization from:
-- "Differential Geometry and Computer Graphics" by Thomas Banchoff
-- in Perspectives in Mathemtaics, Anneversry of Oberwolfasch 1984.
-- Beirkhauser-Verlag, Basel, pp 43-60.

klein(x,y) ==
  cx := cos(x)
  cy := cos(y)
  sx := sin(x)
  sy := sin(y)
  sx2 := sin(x/2)
  cx2 := cos(x/2)
  sq2 := sqrt(2.0@DoubleFloat)
  point [cx * (cx2 * (sq2 + cy) + (sx2 * sy * cy)), _
         sx * (cx2 * (sq2 + cy) + (sx2 * sy * cy)), _
         -sx2 * (sq2 + cy) + cx2 * sy * cy]

draw(klein, 0..4*%pi, 0..2*%pi, var1Steps==50, var2Steps==50, _
     title=="Figure Eight Klein Bottle")

-- Twisted torus

)read ntube

-- rotate a 2-d point by theta round the origin
rotateBy(p, theta) ==
  c := cos(theta)
  s := sin(theta)
  point [p.1*c - p.2*s, p.1*s + p.2*c]

-- a circle in 3-space
bcircle t ==
  point [3*cos t, 3*sin t, 0]

-- an ellipse which twists around 4 times as t revolves once.
twist(u, t) ==
  theta := 4*t
  p := point [sin u, cos(u)/2]
  rotateBy(p, theta)

ntubeDrawOpt(bcircle, twist, 0..2*%pi, 0..2*%pi, _
             var1Steps == 70, var2Steps == 250)

-- Striped torus

-- a twisting circle
twist2(u, t) ==
  theta := t
  p := point [sin u, cos(u)]
  rotateBy(p, theta)

-- color function producing 21 stripes
cf(u,v) == sin(21*u)

ntubeDrawOpt(bcircle, twist2, 0..2*%pi, 0..2*%pi, _
              colorFunction == cf, var1Steps == 168, var2Steps == 126)
