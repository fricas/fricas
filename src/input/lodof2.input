)set break resume
)expose UnittestCount UnittestAux Unittest

testsuite "LODOF2"

testcase "factor"

qF := Expression(Integer)
uP := UnivariatePolynomial('x, qF)
rF := Fraction(uP)
L1 := LinearOrdinaryDifferentialOperator1(rF)
lF := LinearOrdinaryDifferentialOperatorFactorizer2('x, 0)

d := D()$L1
xx := x::uP::rF
x1 := xx^(-1)
fP := factor$ExpressionFactorPolynomial(Integer,qF)

testFactor(l: List L1, y: List qF): Void ==
    free l1
    free l2
    free ll
    free nl
    free nll
    l1 := reduce(*, l)
    nl := #l
    ll := factor(l1, fP, y)$lF
    l2 := reduce(*, ll)
    nll := #ll
    testEquals("nl", "nll")
    testEquals("l1", "l2")

-- reducible order 2 operator (1-1)
testFactor ([d, d+xx], [])

-- irreducible order 2 operator
testFactor ([2*xx^3 * d^2 + 3*xx^2 * d - 2], [])

-- reducible order 3 operator (1-2)
testFactor ([xx-d, -2*xx^3*d^2 - 3*xx^2*d + 2], [])

testFactor ([xx-d, d^2 + xx^2*d + 1], [])

-- reducible order 3 operator (2-1)
testFactor ([-2*xx^3*d^2 - 3*xx^2*d + 2, d - xx], [])

testFactor ([d^2 + xx^2*d + 1, d - xx], [])

-- reducible order 3 operator (1-1-1)
testFactor ([(xx^9 + xx^3)*d +(-sqrt 91+7)*xx^8+(-sqrt 91 + 1)*xx^2, _
            d + ((sqrt 91 + 6)*xx^6 + sqrt 91)/(xx^7+xx), _
            d + (5*xx^6 - 1)/(xx^7 + xx)], [sqrt 91])

-- irreducible order 3 operator
testFactor ([d^3 + 2 * d^2 + 5 / xx * d + 7 / xx^2], [])

-- reducible order 4 operator (2-2)
testFactor ([2*xx^3 * d^2 + 3*xx^2 * d - 2, 2*xx^3 * d^2 + 3*xx^2 * d - 2], [])

testFactor ([d^2 + xx^3 + x1^3, d^2 + xx^2 - x1^3], [])

-- reducible order 5 operator (1-1-3)
testFactor ([d, d, d^3 + xx^2 * d^2 - x1^3 * d + (xx+1) / (xx-1)], [])

testFactor ([d, d, d^3 + xx^2 * d^2 - x1^3 * d + 1], [])

-- reducible order 6 operator (1-1-1-1-1-1)
testFactor ([xx^6*d + (- sqrt(-1) + 5)*xx^5, _
            d + (sqrt(-1)+4)/xx, _
            d + 5/xx, _
            d + 4/xx, _
            d + 3*xx^2/(xx^3-1), _
            d + (-xx^3-2)/(xx^4-xx)], [sqrt(-1)])

-- reducible order 7 operator (2-1-1-3)
testFactor ([d^2 + x1 + xx, d, d, d^3 + xx^2 * d^2 - x1^3 * d + (xx+1) / (xx-1)], [])

statistics()
