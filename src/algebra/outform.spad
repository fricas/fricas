-- Author: Stephen M. Watt
)abbrev package NUMFMT NumberFormats
++ SMW March 88
++ Keywords: string manipulation, roman numerals, format
++ Description:
++ NumberFormats provides function to format and read arabic and
++ roman numbers, to convert numbers to strings and to read
++ floating-point numbers.

NumberFormats() : NFexports == NFimplementation where
    PI ==> PositiveInteger
    I  ==> Integer
    C  ==> Character
    F  ==> Float
    S  ==> String
    V  ==> PrimitiveArray

    NFexports ==> with
        FormatArabic : PI -> S
            ++ FormatArabic(n) forms an Arabic numeral
            ++ string from an integer n.
        ScanArabic :   S -> PI
            ++ ScanArabic(s) forms an integer from an Arabic numeral string s.
        FormatRoman :  PI -> S
            ++ FormatRoman(n) forms a Roman numeral string from an integer n.
        ScanRoman :    S -> PI
            ++ ScanRoman(s) forms an integer from a Roman numeral string s.
        ScanFloatIgnoreSpaces : S -> F
            ++ ScanFloatIgnoreSpaces(s) forms a floating point number from
            ++ the string s ignoring any spaces. Error is generated if the
            ++ string is not recognised as a floating point number.
        ScanFloatIgnoreSpacesIfCan : S -> Union(F, "failed")
            ++ ScanFloatIgnoreSpacesIfCan(s) tries to form a floating point
            ++ number from the string s ignoring any spaces.
        FormatRadix : (I, I) -> S
            ++ FormatRadix(n, r) forms a string from an integer n in radix r.

    NFimplementation ==> add
        import from SExpression
        import from Symbol
        replaceD : C -> C
        replaced : C -> C
        contract : S -> S
        check : S ->Boolean
        replaceD c ==
          if c = char "D" then char "E" else c
        replaced c ==
          if c = char "d" then char "E" else c
        contract s ==
          s := map(replaceD, s)
          s := map(replaced, s)
          ls:List S := split(s,char " ")$String
          s := concat ls
        check s ==
          NUMBERP(READ_-FROM_-STRING(s)$Lisp)$Lisp and
           -- if there is an "E" then there must be a "."
           -- this is not caught by code above
           -- also if the exponent is v.big the above returns false
           not (member?(char "E", s) and not member?(char ".", s) )

--        Original interpreter function:
--        )lis (defun scanstr(x) (spadcomp::|parseFromString| x))
        sexfloat:SExpression := convert(coerce("Float")@Symbol)$SExpression
        ScanFloatIgnoreSpaces s ==
            res := ScanFloatIgnoreSpacesIfCan s
            res case "failed" => error "Non-numeric value"
            res

        ScanFloatIgnoreSpacesIfCan s ==
          s := contract s
          not check s => "failed"
          sex : SExpression :=
              interpret(packageTran(ncParseFromString(s)$Lisp)$Lisp,
                        NIL$Lisp)$Lisp
          sCheck := car(car(sex))
          if (sCheck = sexfloat) = true then
             f := (cdr cdr sex) pretend Float
          else
             if integer?(cdr sex) = true then
                f := (cdr sex) pretend Integer
                f::F
             else
                "failed"

        units : V S :=
           construct ["","I","II","III","IV","V","VI","VII","VIII","IX"]
        tens : V S :=
           construct ["","X","XX","XXX","XL","L","LX","LXX","LXXX","XC"]
        hunds : V S :=
           construct ["","C","CC","CCC","CD","D","DC","DCC","DCCC","CM"]
        umin := minIndex units
        tmin := minIndex tens
        hmin := minIndex hunds
        romval : V I := new(256, -1)
        romval ord char(" ")$C := 0
        romval ord char("I")$C := 1
        romval ord char("V")$C := 5
        romval ord char("X")$C := 10
        romval ord char("L")$C := 50
        romval ord char("C")$C := 100
        romval ord char("D")$C := 500
        romval ord char("M")$C := 1000
        thou:C  := char "M"
        plen:C  := char "("
        pren:C  := char ")"
        ichar:C := char "I"

        FormatArabic n == convert(n)
        ScanArabic   s == qcoerce(parse_integer(s)$ScanningUtilities)

        FormatRoman pn ==
            n := pn::Integer
            -- Units
            d := (n rem 10) + umin
            n := n quo 10
            s := units.d
            zero? n => s
            -- Tens
            d := (n rem 10) + tmin
            n := n quo 10
            s := concat(tens.d, s)
            zero? n => s
            -- Hundreds
            d := (n rem 10) + hmin
            n := n quo 10
            s := concat(hunds.d, s)
            zero? n => s
            -- Thousands
            d := n rem 10
            n := n quo 10
            s := concat(new(d::NonNegativeInteger, thou), s)
            zero? n => s
            -- Ten thousand and higher
            for i in 2.. while not zero? n repeat
                -- Coefficient of 10^(i+2)
                d := n rem 10
                n := n quo 10
                zero? d => iterate
                m0:String := concat(new(i,plen),concat("I",new(i,pren)))
                mm := concat([m0 for j in 1..d]$List(String))
                -- strictly speaking the blank is gratuitous
                if #s > 0 then s := concat(" ", s)
                s  := concat(mm, s)
            s

        -- ScanRoman
        --
        -- The Algorithm:
        --    Read number from right to left.  When the current
        --    numeral is lower in magnitude than the previous maximum
        --    then subtract otherwise add.
        --    Shift left and repeat until done.

        ScanRoman s ==
            s      := upperCase s
            tot : I := 0
            Max : I := 0
            i :   I := maxIndex s
            while i >= minIndex s repeat
                -- Read a single roman digit
                c := s.i; i := i-1
                n := romval ord c
                -- (I)=1000, ((I))=10000, (((I)))=100000, etc
                if n < 0 then
                    c ~= pren =>
                       error concat("Improper character in Roman numeral: ",
                                    new(1, c)$String)
                    nprens : PI := 1
                    while c = pren and i >= minIndex s repeat
                       c := s.i; i := i-1
                       if c = pren then nprens := nprens+1
                    c ~= ichar =>
                       error "Improper Roman numeral: (x)"
                    for k in 1..nprens while i >= minIndex s repeat
                       c := s.i; i := i-1
                       c ~= plen =>
                          error "Improper Roman numeral: unbalanced ')'"
                    n := 10^(nprens + 2)
                if n < Max then
                    tot := tot - n
                else
                    tot := tot + n
                    Max := n
            tot <= 0 => error concat("Improper Roman numeral: ",
                                     convert(tot)@String)
            tot::PI

        FormatRadix(n, r) == write_to_string_radix(n, r)$Lisp

)abbrev domain OUTFORM OutputForm
++ Keywords: output, I/O, expression
++ SMW March/88
++ Description:
++ This domain is used to create and manipulate mathematical expressions
++ for output.  It is intended to provide an insulating layer between
++ the expression rendering software (e.g.FORTRAN, TeX, or Script) and
++ the output coercions in the various domains.

OutputForm() : Join(SetCategory, ConvertibleTo InputForm) with
        --% Printing
        print  : % -> Void
          ++ print(u) prints the form u.
        message : String -> %
          ++ message(s) creates an form with no string quotes
          ++ from string s.
        messagePrint : String -> Void
          ++ messagePrint(s) prints s without string quotes. Note:
          ++ \spad{messagePrint(s)} is equivalent to \spad{print message(s)}.
        --% Creation of atomic forms
        outputForm : Integer -> %
          ++ outputForm(n) creates a form for an integer.
        outputForm : Symbol  -> %
          ++ outputForm(s) creates a form for a symbol.
        outputForm : String  -> %
          ++ outputForm(s) creates a form for a string.
        empty   : () -> %
          ++ empty() creates an empty form.

         --% Space manipulations
        hspace : Integer -> %  ++ hspace(n) creates white space of width n.
        vspace : Integer -> %  ++ vspace(n) creates white space of height n.
        rspace : (Integer, Integer) -> %
          ++ rspace(n, m) creates rectangular white space, n wide by m high.
        --% Area adjustments
        left : (%, Integer) -> %
          ++ left(f, n) left-justifies form f within space of width n.
        right : (%, Integer) -> %
          ++ right(f, n) right-justifies form f within space of width n.
        center : (%, Integer) -> %
          ++ center(f, n) centers form f within space of width n.
        left : % -> %
          ++ left(f) left-justifies form f in total space.
        right : % -> %
          ++ right(f) right-justifies form f in total space.
        center :   % -> %
          ++ center(f) centers form f in total space.

        --% Area manipulations
        hconcat :  (%, %) -> %
          ++ hconcat(f, g) horizontally concatenate forms f and g.
        vconcat :  (%, %) -> %
          ++ vconcat(f, g) vertically concatenates forms f and g.
        hconcat :  List % -> %
          ++ hconcat(u) horizontally concatenates all forms in list u.
        vconcat :  List % -> %
          ++ vconcat(u) vertically concatenates all forms in list u.

        --% Application formers
        prefix :  (%, List %) -> %
          ++ prefix(f, l) creates a form depicting the n-ary prefix
          ++ application of f to a tuple of arguments given by list l.
        infix :   (%, List %) -> %
          ++ infix(f, l) creates a form depicting the n-ary application
          ++ of infix operation f to a tuple of arguments l.
        infix :   (%, %, %) -> %
          ++ infix(op, a, b) creates a form which prints as: a op b.
        postfix : (%, %)    -> %
          ++ postfix(op, a)  creates a form which prints as: a op.
        infix? : % -> Boolean
          ++ infix?(op) returns true if op is an infix operator,
          ++ and false otherwise.
        elt :     (%, List %) -> %
          ++ elt(op, l) creates a form for application of op
          ++ to list of arguments l.

        --% Special forms
        label :   (%, %) -> %
          ++ label(n, f) gives form f an equation label n.
        box :     % -> %
          ++ box(f) encloses f in a box.
        matrix :  List List % -> %
          ++ matrix(llf) makes llf (a list of lists of forms) into
          ++ a form which displays as a matrix.
        zag :     (%, %) -> %
          ++ zag(f, g) creates a form for the continued fraction form for f over g.
        root :    % -> %
          ++ root(f) creates a form for the square root of form f.
        root :    (%, %) -> %
          ++ root(f, n) creates a form for the nth root of form f.
        over :    (%, %) -> %
          ++ over(f, g) creates a form for the vertical fraction of f over g.
        slash :   (%, %) -> %
          ++ slash(f, g) creates a form for the horizontal fraction of f over g.
        assign :  (%, %) -> %
          ++ assign(f, g) creates a form for the assignment \spad{f := g}.
        rarrow :  (%, %) -> %
          ++ rarrow(f, g) creates a form for the mapping \spad{f -> g}.
        differentiate : (%, NonNegativeInteger) -> %
          ++ differentiate(f, n) creates a form for the nth derivative of f,
          ++ e.g. \spad{f'}, \spad{f''}, \spad{f'''},
          ++ "f super \spad{iv}".
        binomial : (%, %) -> %
          ++ binomial(n, m) creates a form for the binomial coefficient of n and m.
        tensor :  (%, %) -> %
          ++ tensor(a, b) creates a form for a tensor b

        --% Scripts
        sub :     (%, %) -> %
          ++ sub(f, n) creates a form for f subscripted by n.
        super :   (%, %) -> %
          ++ super(f, n) creates a form for f superscripted by n.
        presub :  (%, %) -> %
          ++ presub(f, n) creates a form for f presubscripted by n.
        presuper : (%, %) -> %
          ++ presuper(f, n) creates a form for f presuperscripted by n.
        scripts : (%, List %) -> %
          ++ \spad{scripts(f, l)} where l is
          ++ \spad{[sub, super, presuper, presub]} creates a form
          ++ for f with scripts on all 4 corners.  If l has less
          ++ than 4 elements corresponding scripts are absent.
        supersub : (%, List %) -> %
            ++ supersub(a, [sub1, super1, sub2, super2, ...])
            ++ creates a form with each subscript aligned
            ++ under each superscript.

        --% Diacritical marks
        quote :   % -> %
          ++ quote(f) creates the form f with a prefix quote.
        dot :     % -> %
          ++ dot(f) creates the form with one dot overhead.
        dot :     (%, NonNegativeInteger) -> %
          ++ dot(f, n) creates the form f with n dots overhead.
        prime :   % -> %
          ++ prime(f) creates the form f followed by a suffix prime (single quote).
        prime :   (%, NonNegativeInteger) -> %
          ++ prime(f, n) creates the form f followed by n primes.
        overbar : % -> %
          ++ overbar(f) creates the form f with an overbar.
        overlabel : (%, %) -> %
          ++ overlabel(x,f) creates the form f with "x overbar" over the top.

        --% Plexes
        sum :     (%)       -> %
          ++ sum(expr) creates the form prefixing expr by a capital sigma.
        sum :     (%, %)    -> %
          ++ sum(expr, lowerlimit) creates the form prefixing expr by
          ++ a capital sigma with a lowerlimit.
        sum :     (%, %, %) -> %
          ++ sum(expr, lowerlimit, upperlimit) creates the form prefixing expr by
          ++ a capital sigma with both a lowerlimit and upperlimit.
        prod :    (%)       -> %
          ++ prod(expr) creates the form prefixing expr by a capital pi.
        prod :    (%, %)    -> %
          ++ prod(expr, lowerlimit) creates the form prefixing expr by
          ++ a capital pi with a lowerlimit.
        prod :    (%, %, %) -> %
          ++ prod(expr, lowerlimit, upperlimit) creates the form prefixing expr by
          ++ a capital pi with both a lowerlimit and upperlimit.
        int :     (%)       -> %
          ++ int(expr) creates the form prefixing expr with an integral sign.
        int :     (%, %)    -> %
          ++ int(expr, lowerlimit) creates the form prefixing expr by an
          ++ integral sign with a lowerlimit.
        int :     (%, %, %) -> %
          ++ int(expr, lowerlimit, upperlimit) creates the form prefixing expr by
          ++ an integral sign with both a lowerlimit and upperlimit.

        --% Matchfix forms
        brace :   % -> %
          ++ brace(f) creates the form enclosing f in braces (curly brackets).
        brace :   List % -> %
          ++ brace(lf) creates the form separating the elements of lf
          ++ by commas and encloses the result in curly brackets.
        bracket : % -> %
          ++ bracket(f) creates the form enclosing f in square brackets.
        bracket : List % -> %
          ++ bracket(lf) creates the form separating the elements of lf
          ++ by commas and encloses the result in square brackets.
        paren :   % -> %
          ++ paren(f) creates the form enclosing f in parentheses.
        paren :   List % -> %
          ++ paren(lf) creates the form separating the elements of lf
          ++ by commas and encloses the result in parentheses.

        --% Separators for aggregates
        pile :     List % -> %
          ++ pile(l) creates the form consisting of the elements of l which
          ++ displays as a pile, i.e. the elements begin on a new line and
          ++ are indented right to the same margin.

        commaSeparate : List % -> %
          ++ commaSeparate(l) creates the form separating the elements of l
          ++ by commas.
        semicolonSeparate :  List % -> %
          ++ semicolonSeparate(l) creates the form separating the elements of l
          ++ by semicolons.
        blankSeparate : List % -> %
          ++ blankSeparate(l) creates the form separating the elements of l
          ++ by blanks.
        --% Specific applications
        "=":     (%, %) -> %
          ++ f = g creates the equivalent infix form.
        "~=":    (%, %) -> %
          ++ f ~= g creates the equivalent infix form.
        "<":     (%, %) -> %
          ++ f < g creates the equivalent infix form.
        ">":     (%, %) -> %
          ++ f > g creates the equivalent infix form.
        "<=":    (%, %) -> %
          ++ f <= g creates the equivalent infix form.
        ">=":    (%, %) -> %
          ++ f >= g creates the equivalent infix form.
        "+":     (%, %) -> %
          ++ f + g creates the equivalent infix form.
        "-":     (%, %) -> %
          ++ f - g creates the equivalent infix form.
        "-":     (%)    -> %
          ++ - f creates the equivalent prefix form.
        "*":     (%, %) -> %
          ++ f * g creates the equivalent infix form.
        "/":     (%, %) -> %
          ++ f / g creates the equivalent infix form.
        "^":    (%, %) -> %
          ++ f ^ g creates the equivalent infix form.
        "rem":   (%, %) -> %
          ++ f rem g creates the equivalent infix form.
        "quo":   (%, %) -> %
          ++ f quo g creates the equivalent infix form.
        "exquo": (%, %) -> %
          ++ exquo(f, g) creates the equivalent infix form.
        "and":   (%, %) -> %
          ++ f and g creates the equivalent infix form.
        "or":    (%, %) -> %
          ++ f or g creates the equivalent infix form.
        "not":   (%)    -> %
          ++ not f creates the equivalent prefix form.
        SEGMENT : (%, %)  -> %
          ++ SEGMENT(x, y) creates the infix form: \spad{x..y}.
        SEGMENT : (%)    -> %
          ++ SEGMENT(x) creates the prefix form: \spad{x..}.

    == add
        import from NumberFormats

        -- Todo:
        --   program forms, greek letters
        --   infix, prefix, postfix, matchfix support in OUT BOOT
        --   labove rabove, corresponding overs.
        --   better super script, overmark, undermark
        --   uniformize integrals, products, etc as plexes.

        cons_form(x : %, l : List(%)) : % == cons(x, l)$List(%) pretend %

        Rep := SExpression

        a, b : %
        l : List %
        s : String
        e : Symbol
        n : Integer
        nn : NonNegativeInteger

        sform :    String  -> %
        eform :    Symbol  -> %
        iform :    Integer -> %

        print x              == mathprint(x)$Lisp
        message s            == (empty? s => empty(); s pretend %)
        messagePrint s       == print message s
        (a : % = b : %) : Boolean  == EQUAL(a, b)$Lisp
        (a : % = b : %) : %        == convert [eform '_=,     a, b]
        coerce(a) : OutputForm  == a pretend OutputForm
        outputForm n          == n pretend %
        outputForm e          == e pretend %
        sform s               == s pretend %
        eform e               == e pretend %
        iform n               == n pretend %

        outputForm s ==
          sform concat(quote()$Character, concat(s, quote()$Character))

        import from Character

        convert(a: %): InputForm ==
            b: SExpression := a pretend SExpression
            if atom?(b)$SExpression then
                if string?(b)$SExpression then
                    c: String := string(b)$SExpression
                    l: Integer := maxIndex(c)
                    if l >= 2 and c.1 = c.l and c.1 = quote()$Character then
                        d: String := c.(2..(maxIndex c)-1)
                        b: SExpression := convert(d)$SExpression
                if not symbol?(b)$SExpression then
                    return convert(b)$InputForm

            convert([convert('QUOTE)$InputForm, convert(b)$InputForm])$InputForm

        width(a : %) : Integer == outformWidth(a)$Lisp
        width0() : Integer == _$LINELENGTH$Lisp

        center(a, w)   == hconcat(hspace((w - width(a)) quo 2), a)
        left(a, w)     == hconcat(a, hspace((w - width(a))))
        right(a, w)    == hconcat(hspace(w - width(a)), a)
        center(a)     == center(a, width0())
        left(a)       == left(a, width0())
        right(a)      == right(a, width0())

        vspace(n) ==
          n = 0 => empty()
          vconcat(sform " ",vspace(n - 1))

        hspace(n) ==
          n = 0 => empty()
          sform(filler_spaces(n)$Lisp)

        rspace(n, m) ==
          n = 0 or m = 0 => empty()
          vconcat(hspace n, rspace(n, m - 1))

        matrix ll ==
            empty?(ll) => bracket([]$List(%))
            lv : List % := [cons_form(eform 'ROW, l) for l in ll]
            cons_form(eform 'MATRIX, cons(eform 'NIL, lv))

        pile l              == cons_form(eform 'SC, l)
        commaSeparate l     == cons_form(eform 'AGGLST,  l)
        semicolonSeparate l == cons_form(eform 'AGGSET,  l)
        blankSeparate l     ==
           c := eform 'CONCATB
           l1 : List % := []
           for u in reverse l repeat
               uo := u pretend OutputForm
               if has_op?(uo, 'CONCATB)$OutputFormTools then
                   l2 := arguments(uo)$OutputFormTools pretend List(%)
                   l1 := concat(l2, l1)
               else
                   l1 := cons(u, l1)
           cons_form(c, l1)

        brace a        == convert [eform 'BRACE, a]
        brace l        == brace commaSeparate l
        bracket a      == convert [eform 'BRACKET, a]
        bracket l      == bracket commaSeparate l
        paren a        == convert [eform 'PAREN, a]
        paren l        == paren commaSeparate l

        sub(a, b)  == convert [eform 'SUB, a, b]
        super(a, b) == convert [eform 'SUPERSUB, a, sform " ", b]
        presub(a, b) == convert [eform 'SUPERSUB, a, sform " ", sform " ",_
                                  sform " ", b]
        presuper(a, b) == convert [eform 'SUPERSUB, a, sform " ", sform " ", b]
        scripts(a, l) ==
            empty?(l) => a
            empty?(rest(l)) => sub(a, first(l))
            cons_form(eform 'SUPERSUB, cons(a, l))
        supersub(a, l) ==
            if odd?(#l) then l := append(l, [empty()])
            cons_form(eform 'ALTSUPERSUB, cons(a, l))

        hconcat(a, b)  == convert [eform 'CONCAT, a, b]
        hconcat l     == cons_form(eform 'CONCAT, l)
        vconcat(a, b)  == convert [eform 'VCONCAT, a, b]
        vconcat l     == cons_form(eform 'VCONCAT, l)

        (a ~= b) : % == convert [eform '_~_=,    a, b]
        a < b       == convert [eform '_<,     a, b]
        a > b       == convert [eform '_>,     a, b]
        a <= b      == convert [eform '_<_=,    a, b]
        a >= b      == convert [eform '_>_=,    a, b]

        a + b       == convert [eform '_+,     a, b]
        a - b       == convert [eform '_-,     a, b]
        - a         == convert [eform '_-,     a]
        a * b       == convert [eform '_*,     a, b]
        a / b       == convert [eform '_/,     a, b]
        a ^ b       == convert [eform '_^,     a, b]
        a rem b     == convert [eform '_rem,   a, b]
        a quo b     == convert [eform '_quo,   a, b]
        a exquo b   == convert [eform '_exquo, a, b]
        a and b     == convert [eform '_and,   a, b]
        a or b      == convert [eform '_or,    a, b]
        not a       == convert [eform '_not,   a]
        SEGMENT(a, b) == convert [eform 'SEGMENT, a, b]
        SEGMENT(a)  == convert [eform 'SEGMENT, a]
        binomial(a, b) == convert [eform 'BINOMIAL, a, b]

        empty() == convert [eform 'NOTHING]

        infix? a ==
            e : % :=
                IDENTP$Lisp a => a
                STRINGP$Lisp a => INTERN$Lisp a
                return false
            if GET(e, QUOTE(INFIXOP$Lisp)$Lisp)$Lisp then true else false

        elt(a, l) ==
            cons_form(a, l)
        prefix(a, l)   ==
            not infix? a => cons_form(a, l)
            hconcat(a, paren commaSeparate l)
        infix(a, l) ==
            empty?(l) => empty()
            empty?(rest(l)) => first(l)
            infix? a => cons_form(a, l)
            hconcat [first l, a, infix(a, rest l)]
        infix(a, b, c)  ==
            infix? a => convert [a, b, c]
            hconcat [b, a, c]
        postfix(a, b) ==
            hconcat(b, a)

        add_prime(a : %, s : %) : % == convert [eform 'PRIME, a, s]
        quote  a   == convert [eform 'QUOTE,   a]
        overbar a  == convert [eform 'OVERBAR, a]
        dot a      == super(a, sform ".")
        prime a    == prime(a, 1)
        dot(a,nn)   == (s := new(nn, char "."); super(a, sform s))
        prime(a, nn) == (s := new(nn, char ",");  add_prime(a, sform s))

        overlabel(a, b) == convert [eform 'OVERLABEL, a, b]
        box a      == convert [eform 'BOX,     a]
        zag(a, b)  == convert [eform 'ZAG,     a, b]
        root a     == convert [eform 'ROOT,    a]
        root(a, b) == convert [eform 'ROOT,    a, b]
        over(a, b) == convert [eform 'OVER,    a, b]
        slash(a, b) == convert [eform 'SLASH,   a, b]
        assign(a, b) == convert [eform 'LET,     a, b]

        label(a, b) == convert [eform 'EQUATNUM, a, b]
        rarrow(a, b)== convert [eform("~>"::Symbol), a, b]
        differentiate(a, nn)==
            zero? nn => a
            nn < 4 => prime(a, nn)
            r := FormatRoman(nn::PositiveInteger)
            s := lowerCase(r::String)
            add_prime(a, paren sform s)

        sum(a)     == convert [eform 'SIGMA,  empty(), a]
        sum(a, b)  == convert [eform 'SIGMA,  b, a]
        sum(a, b, c) == convert [eform 'SIGMA2, b, c, a]
        prod(a)    == convert [eform 'PI,     empty(), a]
        prod(a, b) == convert [eform 'PI,     b, a]
        prod(a, b, c) == convert [eform 'PI2,    b, c, a]
        int(a)     == convert [eform 'INTSIGN, empty(), empty(), a]
        int(a, b)   == convert [eform 'INTSIGN, b, empty(), a]
        int(a, b, c) == convert [eform 'INTSIGN, b, c, a]
        tensor(a, b) == convert [eform 'TENSOR, a, b]


--Copyright (c) 1991-2002, The Numerical ALgorithms Group Ltd.
--All rights reserved.
--
--Redistribution and use in source and binary forms, with or without
--modification, are permitted provided that the following conditions are
--met:
--
--    - Redistributions of source code must retain the above copyright
--      notice, this list of conditions and the following disclaimer.
--
--    - Redistributions in binary form must reproduce the above copyright
--      notice, this list of conditions and the following disclaimer in
--      the documentation and/or other materials provided with the
--      distribution.
--
--    - Neither the name of The Numerical ALgorithms Group Ltd. nor the
--      names of its contributors may be used to endorse or promote products
--      derived from this software without specific prior written permission.
--
--THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
--IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
--TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
--PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER
--OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
--EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
--PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
--PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
--LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
--NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
--SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
